cmake_minimum_required(VERSION 3.16)
project(ChatSDKUIPlugin VERSION 1.0.0 LANGUAGES CXX)

# C++ and Qt settings
set(CMAKE_CXX_STANDARD 17)
set(CMAKE_CXX_STANDARD_REQUIRED ON)
set(CMAKE_AUTOMOC ON)
set(CMAKE_AUTORCC ON)
set(CMAKE_AUTOUIC ON)

# Require dependency roots (provided by nix)
if(NOT DEFINED LOGOS_LIBLOGOS_ROOT)
    message(FATAL_ERROR "LOGOS_LIBLOGOS_ROOT must be defined")
endif()
if(NOT DEFINED LOGOS_CPP_SDK_ROOT)
    message(FATAL_ERROR "LOGOS_CPP_SDK_ROOT must be defined")
endif()

# Find Qt
find_package(Qt6 REQUIRED COMPONENTS Core Widgets RemoteObjects)

# Component interfaces
find_package(component-interfaces QUIET)
if(NOT component-interfaces_FOUND)
    add_library(component-interfaces INTERFACE)
    target_include_directories(component-interfaces INTERFACE ${CMAKE_CURRENT_SOURCE_DIR}/interfaces)
endif()

# Source files (plugin only, SDK is pre-built)
set(SOURCES
    ChatSDKUIComponent.cpp
    src/ChatSDKWindow.cpp
    src/ConversationListPanel.cpp
    src/ChatPanel.cpp
    src/MessageBubble.cpp
    resources/resources.qrc
)

# Generated code location (pre-generated by nix)
set(PLUGINS_OUTPUT_DIR "${CMAKE_CURRENT_SOURCE_DIR}/generated_code")
list(APPEND SOURCES ${PLUGINS_OUTPUT_DIR}/logos_sdk.cpp)

# Create plugin library
add_library(chatsdk_ui SHARED ${SOURCES})

set_target_properties(chatsdk_ui PROPERTIES
    PREFIX ""
    OUTPUT_NAME "chatsdk_ui"
)

# Include directories (installed layout)
target_include_directories(chatsdk_ui PRIVATE
    ${CMAKE_CURRENT_SOURCE_DIR}
    ${CMAKE_CURRENT_BINARY_DIR}
    ${PLUGINS_OUTPUT_DIR}
    ${LOGOS_LIBLOGOS_ROOT}/include
    ${LOGOS_CPP_SDK_ROOT}/include
    ${LOGOS_CPP_SDK_ROOT}/include/cpp
    ${LOGOS_CPP_SDK_ROOT}/include/core
    ${PLUGINS_OUTPUT_DIR}/include
)

# Link libraries
find_library(LOGOS_SDK_LIB logos_sdk PATHS ${LOGOS_CPP_SDK_ROOT}/lib NO_DEFAULT_PATH REQUIRED)
target_link_libraries(chatsdk_ui PRIVATE
    Qt6::Core
    Qt6::Widgets
    Qt6::RemoteObjects
    component-interfaces
    ${LOGOS_SDK_LIB}
)

# Output directories
set_target_properties(chatsdk_ui PROPERTIES
    LIBRARY_OUTPUT_DIRECTORY "${CMAKE_BINARY_DIR}/modules"
    RUNTIME_OUTPUT_DIRECTORY "${CMAKE_BINARY_DIR}/modules"
    BUILD_WITH_INSTALL_RPATH TRUE
    SKIP_BUILD_RPATH FALSE
)

# Platform-specific RPATH
if(APPLE)
    set_target_properties(chatsdk_ui PROPERTIES
        INSTALL_RPATH "@loader_path"
        INSTALL_NAME_DIR "@rpath"
        BUILD_WITH_INSTALL_NAME_DIR TRUE
    )
    add_custom_command(TARGET chatsdk_ui POST_BUILD
        COMMAND install_name_tool -id "@rpath/chatsdk_ui.dylib" $<TARGET_FILE:chatsdk_ui>
    )
else()
    set_target_properties(chatsdk_ui PROPERTIES
        INSTALL_RPATH "$ORIGIN"
        INSTALL_RPATH_USE_LINK_PATH FALSE
    )
endif()

# Install rules
install(TARGETS chatsdk_ui
    LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR}/logos/modules
    RUNTIME DESTINATION ${CMAKE_INSTALL_LIBDIR}/logos/modules
    ARCHIVE DESTINATION ${CMAKE_INSTALL_LIBDIR}/logos/modules
)

install(FILES ${CMAKE_CURRENT_SOURCE_DIR}/metadata.json
    DESTINATION ${CMAKE_INSTALL_DATADIR}/logos-chatsdk-ui
)

message(STATUS "Chat SDK UI Plugin configured successfully")
